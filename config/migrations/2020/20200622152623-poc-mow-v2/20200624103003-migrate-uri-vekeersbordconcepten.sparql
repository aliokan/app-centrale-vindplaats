PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>

DELETE {
  GRAPH ?g {
    ?bord a mobiliteit:Verkeersbordconcept;
             ?p ?o.
    ?subjectPointTo <http://data.lblod.info/vocabularies/mobiliteit/verkeersbordconcept> ?bord.
  }
}
INSERT {
  GRAPH ?g {
  ?nieuwbord a mobiliteit:Verkeersbordconcept;
              ?p ?o.
  ?subjectPointTo <http://data.lblod.info/vocabularies/mobiliteit/verkeersbordconcept> ?nieuwbord.
  }
}
WHERE {
  GRAPH ?g {
    ?bord a mobiliteit:Verkeersbordconcept;
           skos:prefLabel ?code;
           ?p ?o.

    OPTIONAL {
      ?subjectPointTo <http://data.lblod.info/vocabularies/mobiliteit/verkeersbordconcept> ?bord.
    }
  }

  BIND(IRI(CONCAT("http://data.mow.vlaanderen.be/id/concept/",?code)) AS ?nieuwbord)
}